#!/bin/sh
# Copyright 2019, Development Gateway. See LICENSE.

UNIT=$((1024 ** 3))
: ${SR_TYPE:=lvmohba}
TRANSPOSE_ROWS='
function flush() {
  if (!need_flush) return;

  output_line = "";

  for (i = 1; i <= num_fields; i++) {
    separator = (i > 1) ? OFS : "";
    output_line = output_line separator current_object[field_names[i]];
  }

  print output_line;
  need_flush = 0;
}

BEGIN {
  FS=" +[(][ [:upper:]]+[)]( *): ";
  OFS="\t";
  need_flush = 0;
  num_fields = split(fields, field_names, ",");
}

{
  if (NF == 0) {
    flush();
  } else {
    gsub(" +", "", $1);
    current_object[$1] = (unit && $2 ~ "^[[:digit:]]+$") ? int($2 / unit) : $2;
    need_flush = 1;
  }
}

END {
  flush();
}
'

xe_status() {
  PARAMS="$1"
  shift
  {
    echo "$PARAMS" | tr ',' '\t'
    xe $@ params="$PARAMS" \
      | awk -v fields="$PARAMS" -v unit=$UNIT "$TRANSPOSE_ROWS" \
      | sort -k 1
  } | column -ts $'\t'
}

xe_status name-label,memory-total,memory-free host-list
xe_status name-label,physical-size,physical-utilisation sr-list type=$SR_TYPE
